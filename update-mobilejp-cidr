#!/usr/bin/env perl

use strict;
use warnings;

use Carp;
use Digest::MD5::File qw(file_md5);
use Getopt::Long;
use Pod::Usage;

use WWW::MobileCarrierJP 0.19;
use WWW::MobileCarrierJP::DoCoMo::CIDR;
use WWW::MobileCarrierJP::EZWeb::CIDR;
use WWW::MobileCarrierJP::AirHPhone::CIDR;
use WWW::MobileCarrierJP::ThirdForce::CIDR;

my @Carriers = qw/DoCoMo EZWeb AirHPhone ThirdForce/;

&main;exit 9;

sub short_name_for {
    my $carrier = shift;
    +{
        DoCoMo     => 'docomo',
        EZWeb      => 'ezweb',
        AirHPhone  => 'willcom',
        ThirdForce => 'softbank',
    }->{$carrier};
}

sub scrape {
    my $result;
    for my $carrier (@Carriers) {
        my $class = "WWW::MobileCarrierJP::${carrier}::CIDR";
        my $dat;
        my $retry = 3;

        while ($retry-- >= 0) {
            $dat = $class->scrape;
            last if @$dat > 0;
        };
        if (@$dat <= 0) {
            carp "[ERROR] cannot scrape CIDR for $carrier";
            next;
        }
        $result->{short_name_for($carrier)} = [map { "$_->{ip}$_->{subnetmask}" } @$dat];
    }
    return $result;
}

sub main {
    my $output_dir = "";

    Getopt::Long::Configure("bundling");
    GetOptions(
               'output_dir|o=s' => \$output_dir,
               'help|h|?' => sub{ pod2usage(-verbose=>1) }) or pod2usage();
    -d $output_dir || pod2usage("no such directory: $output_dir");


    my $result = scrape();

    my $exit_code = 1;
    while (my($bname, $cidrs) = each %$result) {
        my $filename     = join '/', $output_dir, $bname;
        my $filename_tmp = $filename . '.tmp';

        open my $fh, '>', $filename_tmp or do {
            carp "failed to open file: $filename_tmp: $!";
            next;
        };
        print $fh "$_\n" for sort byoctet @$cidrs;
        close $fh;

        if ( updated_cidr($filename, $filename_tmp) ) {
            $exit_code = 0;
            rename $filename_tmp, $filename or carp $!;
        } else {
            unlink $filename_tmp or carp $!;
        }
    }

    exit $exit_code;
}

sub byoctet {
    my $ao = [split /\./, $a];
    my $bo = [split /\./, $b];
    $ao->[0] <=> $bo->[0] or
    $ao->[1] <=> $bo->[1] or
    $ao->[2] <=> $bo->[2] or
    $ao->[3] <=> $bo->[3];
}

sub updated_cidr {
    my($old, $new) = @_;
    return ! -e $old || file_md5($old) ne file_md5($new);
}

__END__

=head1 SYNOPSIS

B<update-mobilejp-cidr> B<-o> I<OUTPUT_DIR>

    $ update-mobilejp-cidr -o /var/ip.d/plain
        output into /var/ip.d/plain/{docomo,ezweb,willcom,softbank}

    $ update-mobilejp-cidr -o /var/ip.d/plain && generate-cidr-files ...

=head1 EXIT CODE

If no update cidr then exit(1), otherwise (some cidr updated) exit(0).

